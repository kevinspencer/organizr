#!/usr/bin/env perl
# Copyright 2010 Kevin Spencer <kevin@kevinspencer.org>
#
# Permission to use, copy, modify, distribute, and sell this software and its
# documentation for any purpose is hereby granted without fee, provided that
# the above copyright notice appear in all copies and that both that
# copyright notice and this permission notice appear in supporting
# documentation.  No representations are made about the suitability of this
# software for any purpose.  It is provided "as is" without express or 
# implied warranty.
#
################################################################################

use File::Basename;
use File::Copy;
use File::Path;
use File::Spec;
use File::stat;
use Time::localtime;
use strict;
use warnings;

our $VERSION = '0.07';

my $user     = getpwuid($<);
my $BASEDIR  = "/Users/$user/Pictures";
my $STOREDIR = File::Spec->catdir($BASEDIR, 'storage');

my %seendir;
processDirectory($BASEDIR);

sub processDirectory {
    my $dir = shift;

    return if ($dir =~ /$STOREDIR/);
    opendir(DIR, $dir) || die "Could not open $dir - $!\n";
    for my $filefound (grep /\.JPG$/i, readdir(DIR)) {
        my $fileFoundFullPath = File::Spec->catfile($dir, $filefound);
        if (-d $fileFoundFullPath) {
            processDirectory($fileFoundFullPath);
        } else {
            processFile($fileFoundFullPath);
        }
    }
}

sub processFile {
    my $fileFoundFullPath = shift;

    my $stat = stat($fileFoundFullPath);
    my $time = localtime($stat->mtime());
    my $YYYY = $time->year() + 1900;
    my $MM   = sprintf("%02d", $time->mon() + 1);
    my $finaldir = File::Spec->catdir($STOREDIR, $YYYY, "$YYYY-$MM");
    if (! $seendir{$finaldir}++) {
        if (! -d $finaldir) {
            eval { mkpath($finaldir) };
            if ($@) {
                die $@;
            }
        }
    }
    # make sure we don't clobber an existing file...
    my $finalfile = 
        File::Spec->catfile($finaldir, basename($fileFoundFullPath));
    if (-e $finalfile) {
        # file already exists so we'll just add a sequence # and rename it
        my $seq = 0;
        while(1) {
            $seq++;
            my ($name, undef, $suffix) = 
                fileparse($fileFoundFullPath, qr/\.[^.]*/);
            $name = $name . '.' . $seq . $suffix;
            $finalfile = File::Spec->catfile($finaldir, $name);
            next if (-e $finalfile);
            my $newFileFullPath = 
                File::Spec->catfile(dirname($fileFoundFullPath), $name);
            rename($fileFoundFullPath, $newFileFullPath) ||
                die "Could not rename $fileFoundFullPath to $newFileFullPath $!\n";
            $fileFoundFullPath = $newFileFullPath;
            last;
        }
    }
    move($fileFoundFullPath, $finaldir) ||
        die "Couldn't move $fileFoundFullPath to $finaldir - $!\n";
}
